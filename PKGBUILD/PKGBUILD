# Maintainer: Anton Hvornum anton@hvornum.se
# Contributor: Anton Hvornum anton@hvornum.se
# Contributor: Fabian Bornschein plusfabi-cat-gmail-dog-com
pkgbase=archinstall-gui
pkgname=('archinstall-gui-server' 'archinstall-gui-standalone' 'archinstall-gui-desktop')
pkgver=v0.1rc20
pkgrel=2
url="https://github.com/Torxed/archinstall_gui"
license=('GPLv3')
arch=('x86_64')
makedepends=(git)
_archinstall_commit=2c24903932342a32cdfa8c684547859116fb254e		# tags/v2.0.5
_archinstall_gui_commit=2248aeb3099c296baf873c5e45aa181f09346189	# tags/v0.1rc20
source=(git+https://github.com/Torxed/archinstall.git#commit=${_archinstall_commit}
		git+https://github.com/Torxed/archinstall_gui.git#commit=${_archinstall_gui_commit}
		archinstall-gui
		archinstall_gui.service
		archinstall_gui.svg
		org.archlinux.archinstall_gui.desktop)
sha256sums=('SKIP'
            'SKIP'
            '78ea37402accdb4584b0d48618add5f074bf353a29f85ad0cae4dd3a4b5be323'
            'eb0acdf595173f97b7c1ef6ff92cd62c63fd4e0e7d870087c65b2f3f1bb6438c'
            '31d5f6a03ddcac20d81696ba717ceafe63caf592491970ceafa7d5717c1c2224'
            '3943d9349c71456bf6bf72c90ba39a432f78df6dbe99969fa937bd14deee498f')

pkgver() {
  cd "archinstall_gui"
  git describe --tags | sed 's/-/+/g'
}

package_archinstall-gui-server() {
	pkgdesc="Server for a graphical installer for Arch Linux."
	depends=('python>=3.8' 'systemd')
	optdepends=('python-systemd: Adds more controlled logging functionality')

	mkdir -p "${pkgdir}/srv/archinstall_gui"
	mkdir -p "${pkgdir}/usr/bin"

	mv "${srcdir}/archinstall_gui/archinstall_gui"/* "${pkgdir}/srv/archinstall_gui/"
	mv "${srcdir}/archinstall/archinstall" "${pkgdir}/srv/archinstall_gui/dependencies/"

	chmod +x "${pkgdir}/srv/archinstall_gui/webgui.py"

	install -Dm 644 "${srcdir}/archinstall_gui.service" \
		"${pkgdir}/usr/lib/systemd/system/archinstall_gui.service"
}

package_archinstall-gui-standalone() {
	pkgdesc="A graphical installer for Arch Linux."
	depends=('archinstall-gui-server' 'chromium' 'xorg-server' 'xorg-xinit')

	install -Dm 755 "${srcdir}/archinstall-gui" \
		"${pkgdir}/usr/bin/archinstall-gui"
}

package_archinstall-gui-desktop() {
	pkgdesc="A graphical installer for Arch Linux."
	depends=('archinstall-gui-server' 'xdg-utils')

	install -Dm 644 "${srcdir}/archinstall_gui.svg" \
		"${pkgdir}/usr/share/pixmaps/archinstall_gui.svg"
	install -Dm 644 "${srcdir}/org.archlinux.archinstall_gui.desktop" \
		"${pkgdir}/usr/share/applications/org.archlinux.archinstall_gui.desktop"	
}
